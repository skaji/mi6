#!/usr/bin/env perl6
use v6;
use App::Mi6;

my $app = App::Mi6.new;

multi MAIN("new", $module) {
    $app.cmd("new", $module);
}
multi MAIN("build") {
    $app.cmd("build");
}
multi MAIN("test", *@file, Bool :v(:$verbose), Int :j(:$jobs)) {
    $app.cmd("test", @file, :$verbose, :$jobs);
}
multi MAIN("release") {
    $app.cmd("release");
}
multi MAIN("help") { usage }
multi MAIN(:h(:$help)!) { usage}
multi MAIN(|) { usage; exit 1; }
sub usage() {
    require Pod::To::Text;
    ::("Pod::To::Text").render($=pod).say;
}

=begin pod

=head1 USAGE

  > mi6 new Foo::Bar # create Foo-Bar distribution
  > mi6 build        # build the distribution, and re-generate README.md/META6.json
  > mi6 test         # run tests
  > mi6 release      # show release steps

  Examples:
  > mi6 --verbose test    # run tests with verbose mode (alias: -v)
  > mi6 --jobs=N  test    # run N test jobs in parallel (alias: -j)
  > mi6 test t/02-test.t  # run specific test

=end pod
